apply plugin: 'io.gitlab.arturbosch.detekt'

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:0.45.2"
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    mainClass = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args = [
        "-F",
        "--android",
        "--disabled_rules",
        "no-wildcard-imports,max-line-length",
        "src/**/*.kt"
    ]
}

detekt {
    reports {
        xml {
            enabled = false
        }
        txt {
            enabled = false
        }
        html {
            enabled = false
        }
    }
    config = files("$rootDir/config/detekt/detekt.yml")
    source = files("src/main/java")
    baseline = file("detekt-baseline.xml")
}
